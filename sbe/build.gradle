plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

configurations {
    codegen
}

dependencies {
    implementation('io.aeron:aeron-all:1.42.1')
    codegen 'uk.co.real-logic:sbe-tool:1.28.3'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

def generatedDir = file("${buildDir}/generated/src/main/java")

tasks.register('generateMessages', JavaExec) {
    getMainClass().set('uk.co.real_logic.sbe.SbeTool')
    classpath = configurations.getByName("codegen")
    systemProperties('sbe.output.dir': generatedDir,
            'sbe.target.language': 'Java',
            'sbe.validation.stop.on.error': 'true')
    args = [
            'src/main/resources/orderBook.xml'
    ]
}

tasks.compileJava.dependsOn 'generateMessages'

sourceSets {
    main {
        java.srcDir(generatedDir)
    }
}

test {
    useJUnitPlatform()
}